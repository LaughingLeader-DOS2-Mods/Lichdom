INIT
	INT:%LLLICH_SavingThrow_Fear = 500
	INT:%LLLICH_SavingThrow_Stunned = 700

EVENTS

//This effect is disabled/overriden in the status
EVENT LLLICH_Lifesteal_FX
VARS
	CHARACTER:_Character
ON
	OnCharacterStatusAttempt(_Character, LIFESTEAL)
ACTIONS
IF "!c1"
	IsTagged(_Character, "LLLICH_Lich")
THEN
	CharacterPlayEffect(_Character, "RS3_FX_GP_Status_NecroHeal_Impact_01")
ELSE
	CharacterPlayEffect(_Character, "RS3_FX_GP_Impacts_Ghost_01", "Dummy_BodyFX")
ENDIF

EVENT LLLICH_Lichform_Apply
VARS
	CHARACTER:_Lich
ON
	OnCharacterEvent(_Lich, "LLLICH_SetupLich")
	OnCharacterEvent(_Lich, "LLLICH_ApplyStatusInfluence")
ACTIONS
	RemoveStatusInfluence(_Lich, LLLICH_LICHFORM, 1000.0, "", 0)
	//CharacterRemoveStatus(_Lich, LLLICH_LICHFORM)
	AddStatusInfluence(_Lich, LLLICH_LICHFORM, 999.0, "", 0)
	
EVENT LLLICH_Lichform_Remove
VARS
	CHARACTER:_Lich
ON
	OnCharacterEvent(_Lich, "LLLICH_ClearLich")
	OnCharacterEvent(_Lich, "LLLICH_ClearStatusInfluence")
ACTIONS
	RemoveStatusInfluence(_Lich, LLLICH_LICHFORM, 1000.0, "", 0)
	//CharacterRemoveStatus(_Lich, LLLICH_LICHFORM)
	
EVENT LLLICH_LichForm_OnApplied
VARS
	CHARACTER:_Lich
ON
	OnCharacterStatusApplied(_Lich, LLLICH_LICHFORM)
	OnCharacterStatusApplied(_Lich, LLLICH_PHYLACTERY_ACTIVE)
	OnIterateCharacter(_Lich, "LLLICH_Iterator_ApplyPenalty")
	OnCharacterEvent(_Lich, "LLLICH_ApplyLichPenalty")
ACTIONS
IF "!c1"
	HasGlobalFlag("LLLICH_Settings_DisableStatusPenalty")
THEN
	IF "c1"
		CharacterHasStatus(_Lich, LLLICH_PHYLACTERY_ACTIVE)
	THEN
		CharacterApplyStatus(_Lich, LLLICH_LICHFORM_PENALTY_A, -1, 1, _Lich)
	ELSE
		CharacterApplyStatus(_Lich, LLLICH_LICHFORM_PENALTY_B, -1, 1, _Lich)
	ENDIF
ENDIF

EVENT LLLICH_LichForm_OnRemoved
VARS
	CHARACTER:_Lich
ON
	OnCharacterStatusRemoved(_Lich, LLLICH_LICHFORM)
	OnIterateCharacter(_Lich, "LLLICH_Iterator_RemovePenalty")
	OnCharacterEvent(_Lich, "LLLICH_RemoveLichPenalty")
ACTIONS
IF "c1"
	CharacterHasStatus(_Lich, LLLICH_LICHFORM_PENALTY_A)
THEN
	CharacterRemoveStatus(_Lich, LLLICH_LICHFORM_PENALTY_A)
ENDIF
IF "c1"
	CharacterHasStatus(_Lich, LLLICH_LICHFORM_PENALTY_B)
THEN
	CharacterRemoveStatus(_Lich, LLLICH_LICHFORM_PENALTY_B)
ENDIF

EVENT LLLICH_LichForm_Penalties_Apply_IterateStart
ON
	OnGlobalFlagCleared("LLLICH_Settings_DisableStatusPenalty")
ACTIONS
	IterateParty("LLLICH_Iterator_ApplyPenalty", null, null, null, "LLLICH_Lich")

EVENT LLLICH_LichForm_Penalties_Remove_IterateStart
ON
	OnGlobalFlagSet("LLLICH_Settings_DisableStatusPenalty")
ACTIONS
	IterateParty("LLLICH_Iterator_RemovePenalty", null, null, null, "LLLICH_Lich")

EVENT LLLICH_Lichform_InfoStatus_Apply
VARS
	CHARACTER:_Lich
ON
	OnCharacterStatusApplied(_Lich, LLLICH_LICHFORM)
ACTIONS
	CharacterApplyStatus(_Lich, LLLICH_LICHFORM_INFO, -1, 0, _Lich)

EVENT LLLICH_Lichform_InfoStatus_RemoveWorkaround
VARS
	CHARACTER:_Lich
ON
	OnCharacterStatusRemoved(_Lich, LLLICH_LICHFORM_INFO)
ACTIONS
IF "c1&!c2"
	HasStatusInfluence(_Lich, LLLICH_LICHFORM)
	HasFlag(_Lich, "LLLICH_Settings_HideLichStatus")
THEN
	CharacterApplyStatus(_Lich, LLLICH_LICHFORM_INFO, -1, 0, _Lich)
ENDIF
	
EVENT LLLICH_Lichform_InfoStatus_ApplyViaFlag
VARS
	CHARACTER:_Lich
ON
	OnCharacterFlagCleared("LLLICH_Settings_HideLichStatus", _Lich)
ACTIONS
IF "c1"
	CharacterHasStatus(_Lich, LLLICH_LICHFORM)
THEN
	CharacterApplyStatus(_Lich, LLLICH_LICHFORM_INFO, -1, 0, _Lich)
ENDIF

EVENT LLLICH_Lichform_InfoStatus_Remove
VARS
	CHARACTER:_Lich
ON
	OnCharacterStatusRemoved(_Lich, LLLICH_LICHFORM)
	OnCharacterFlagSet("LLLICH_Settings_HideLichStatus", _Lich)
ACTIONS
	CharacterRemoveStatus(_Lich, LLLICH_LICHFORM_INFO, null, 0)

EVENT LLLICH_Lichform_SavingThrow_Fear
VARS
	CHARACTER:_Lich
	INT:_Roll
ON
	OnCharacterStatusApplied(_Lich, FEAR)
ACTIONS
IF "c1"
	CharacterHasStatus(_Lich, LLLICH_LICHFORM)
THEN
	GetRandomBetween(_Roll, 0, 999)
	IF "c1"
		IsGreaterThen(_Roll, %LLLICH_SavingThrow_Fear)
	THEN
		CharacterRemoveStatus(_Lich, FEAR, LLLICH_LICHFORM_INFO, 1)
		StatusText(_Lich, "LLLICH_StatusText_SavingThrow_Fear")
		//CombatLogText(_Lich, "LLLICH_StatusText_SavingThrow_Fear", 1, 1)
		//DisplayText(_Lich, "", 0)
	ENDIF
ENDIF

EVENT LLLICH_Lichform_SavingThrow_Stunned
VARS
	CHARACTER:_Lich
	INT:_Roll
ON
	OnCharacterStatusApplied(_Lich, STUNNED)
ACTIONS
IF "c1"
	CharacterHasStatus(_Lich, LLLICH_LICHFORM)
THEN
	GetRandomBetween(_Roll, 0, 999)
	IF "c1"
		IsGreaterThen(_Roll, %LLLICH_SavingThrow_Stunned)
	THEN
		CharacterRemoveStatus(_Lich, STUNNED, LLLICH_LICHFORM_INFO, 1)
		StatusText(_Lich, "LLLICH_StatusText_SavingThrow_Stunned")
		//CombatLogText(_Lich, "LLLICH_StatusText_SavingThrow_Stunned", 1, 1)
		//DisplayText(_Lich, "", 0)
	ENDIF
ENDIF

//REGION CHILLFORM

/* Stats version:
IF(InSurface:SurfaceWaterFrozen|InSurface:SurfaceWaterFrozenBlessed|InSurface:SurfaceWaterFrozenCursed|InSurface:SurfaceWaterFrozenPurified|InSurface:SurfaceBloodFrozen|InSurface:SurfaceBloodFrozenBlessed|InSurface:SurfaceBloodFrozenCursed|InSurface:SurfaceBloodFrozenPurified|InSurface:SurfaceFrostCloud):LLLICH_CHILLFORM_SPEEDBOOST,100,-2
*/
EVENT LLLICH_ChillForm_SpeedBoost_Apply
VARS
	CHARACTER:_Lich
	FLOAT:_Radius
ON
	OnCharacterStatusApplied(_Lich, INSURFACE)
	OnCharacterStatusApplied(_Lich, LLLICH_CHILLFORM)
ACTIONS
IF "c1&!c2"
	CharacterHasStatus(_Lich, LLLICH_CHILLFORM)
	CharacterHasStatus(_Lich, LLLICH_CHILLFORM_SPEEDBOOST)
THEN
	Set(_Radius, 2.0)
	IF "c1|c2|c3|c4|c5|c6|c7|c8|c9|c10"
		IsInSurface(_Lich, SurfaceWaterFrozen, _Radius)
		IsInSurface(_Lich, SurfaceWaterFrozenBlessed, _Radius)
		IsInSurface(_Lich, SurfaceWaterFrozenCursed, _Radius)
		IsInSurface(_Lich, SurfaceWaterFrozenCursed, _Radius)
		IsInSurface(_Lich, SurfaceWaterFrozenPurified, _Radius)
		IsInSurface(_Lich, SurfaceBloodFrozen, _Radius)
		IsInSurface(_Lich, SurfaceBloodFrozenBlessed, _Radius)
		IsInSurface(_Lich, SurfaceBloodFrozenCursed, _Radius)
		IsInSurface(_Lich, SurfaceBloodFrozenCursed, _Radius)
		IsInSurface(_Lich, SurfaceBloodFrozenPurified, _Radius)
	THEN
		CharacterApplyStatus(_Lich, LLLICH_CHILLFORM_SPEEDBOOST, 1, 1, _Lich)
	ENDIF
ENDIF

EVENT LLLICH_ChillForm_SpeedBoost_RemoveStart
VARS
	CHARACTER:_Lich
ON
	OnCharacterStatusRemoved(_Lich, INSURFACE)
ACTIONS
IF "c1&c2"
	CharacterHasStatus(_Lich, LLLICH_CHILLFORM)
	CharacterHasStatus(_Lich, LLLICH_CHILLFORM_SPEEDBOOST)
THEN
	SetVar(_Lich, "LeaderLib_Timer_CompleteEvent", STRING:"LLLICH_Events_ChillForm_CheckForIceSurface")
	SetVar(_Lich, "LeaderLib_Timer_Delay", INT:899)
	CharacterEvent(_Lich, "LeaderLib_Commands_StartObjectTimer")
ENDIF

EVENT LLLICH_ChillForm_SpeedBoost_Remove
VARS
	CHARACTER:_Lich
	FLOAT:_Radius
ON
	OnCharacterEvent(_Lich, "LLLICH_Events_ChillForm_CheckForIceSurface")
ACTIONS
	IF "!c1"
		CharacterHasStatus(_Lich, INSURFACE)
	THEN
		CharacterRemoveStatus(_Lich, LLLICH_CHILLFORM_SPEEDBOOST, null, 0)
	ELSE
		Set(_Radius, 1.0)
		IF "!c1&!c2&!c3&!c4&!c5&!c6&!c7&!c8&!c9&!c10"
			IsInSurface(_Lich, SurfaceWaterFrozen, _Radius)
			IsInSurface(_Lich, SurfaceWaterFrozenBlessed, _Radius)
			IsInSurface(_Lich, SurfaceWaterFrozenCursed, _Radius)
			IsInSurface(_Lich, SurfaceWaterFrozenCursed, _Radius)
			IsInSurface(_Lich, SurfaceWaterFrozenPurified, _Radius)
			IsInSurface(_Lich, SurfaceBloodFrozen, _Radius)
			IsInSurface(_Lich, SurfaceBloodFrozenBlessed, _Radius)
			IsInSurface(_Lich, SurfaceBloodFrozenCursed, _Radius)
			IsInSurface(_Lich, SurfaceBloodFrozenCursed, _Radius)
			IsInSurface(_Lich, SurfaceBloodFrozenPurified, _Radius)
		THEN
			CharacterRemoveStatus(_Lich, LLLICH_CHILLFORM_SPEEDBOOST, null, 0)
		ENDIF
	ENDIF
EVENT LLLICH_ChillForm_EndTurn_FX
VARS
	CHARACTER:_Lich
ON
	OnCharacterStatusRemoved(_Lich, LLLICH_CHILLFORM_ENDTURN_AURA)
ACTIONS
IF "c1"
	IsInCombat(_Lich)
THEN
	PlayEffectAt(_Lich, "RS3_FX_Skills_Water_Ice_Shout_Ground_02")
ENDIF
	
EVENT LLLICH_ChillForm_Removed
VARS
	CHARACTER:_Lich
	FLOAT:_Radius
ON
	OnCharacterStatusRemoved(_Lich, LLLICH_CHILLFORM)
ACTIONS
	CharacterRemoveStatus(_Lich, LLLICH_CHILLFORM_ENDTURN_AURA, null, 0)
	CharacterRemoveStatus(_Lich, LLLICH_CHILLFORM_SPEEDBOOST, null, 0)	

//END_REGION