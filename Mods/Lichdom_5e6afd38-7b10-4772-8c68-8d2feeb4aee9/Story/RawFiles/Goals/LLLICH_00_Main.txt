Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LLLICH_UndeadConversionTags("Dwarf", "UNDEAD_DWARF", "DWARF");
DB_LLLICH_UndeadConversionTags("Human", "UNDEAD_HUMAN", "HUMAN");
DB_LLLICH_UndeadConversionTags("Elf", "UNDEAD_ELF", "ELF");
DB_LLLICH_UndeadConversionTags("Lizard", "UNDEAD_LIZARD", "LIZARD");

/* Original Undead
Dwarves_Hero_Female_Undead_373a1966-a54d-4a3e-be70-e779a654c914
Dwarves_Hero_Male_Undead_726442a5-6856-4b0d-91ed-5d2f003b8a0c
Elves_Hero_Female_Undead_7f366172-9fd1-45df-8719-a6d14cb534b3
Elves_Hero_Male_Undead_9eeaaafd-c47d-4650-9200-b00430d61e83
Humans_Hero_Female_Undead_3bd0693d-0b0a-4f6d-93e2-aea9be654bee
Humans_Hero_Male_Undead_5ab5d036-4606-4265-962e-c2e4d2d2408b
Lizards_Hero_Female_Undead_725f9a47-a3d4-41d2-92cf-017d18c2b212
Lizards_Hero_Male_Undead_57b70554-36bf-4b86-b9aa-8f7cc3944153

DB_LLLICH_RaceConversion("Dwarf", 1, "Dwarves_Hero_Female_Undead_373a1966-a54d-4a3e-be70-e779a654c914");
DB_LLLICH_RaceConversion("Dwarf", 0, "Dwarves_Hero_Male_Undead_726442a5-6856-4b0d-91ed-5d2f003b8a0c");
DB_LLLICH_RaceConversion("Elf", 1, "Elves_Hero_Female_Undead_7f366172-9fd1-45df-8719-a6d14cb534b3");
DB_LLLICH_RaceConversion("Elf", 0, "Elves_Hero_Male_Undead_9eeaaafd-c47d-4650-9200-b00430d61e83");
DB_LLLICH_RaceConversion("Human", 1, "Humans_Hero_Female_Undead_3bd0693d-0b0a-4f6d-93e2-aea9be654bee");
DB_LLLICH_RaceConversion("Human", 0, "Humans_Hero_Male_Undead_5ab5d036-4606-4265-962e-c2e4d2d2408b");
DB_LLLICH_RaceConversion("Lizard", 1, "Lizards_Hero_Female_Undead_725f9a47-a3d4-41d2-92cf-017d18c2b212");
DB_LLLICH_RaceConversion("Lizard", 0, "Lizards_Hero_Male_Undead_57b70554-36bf-4b86-b9aa-8f7cc3944153");

Lich Versions
Dwarves_Hero_Female_Undead_Lich_414c29bd-6a1a-42ee-a885-f7d449d911f8
Dwarves_Hero_Male_Undead_Lich_a9f4b776-dab2-4204-a28c-37fb592dac35
Elves_Hero_Female_Undead_Lich_9dfe13c2-a2f6-4cbb-b341-70f58baded63
Elves_Hero_Male_Undead_Lich_4305d9f2-1190-4de9-81a5-2c3b10104bd1
Humans_Hero_Female_Undead_Lich_8ff91227-7d47-4f0a-bf68-0a800a6fa250
Humans_Hero_Male_Undead_Lich_8ce7cc69-220c-4c49-9723-f28c50750ddf
Lizards_Hero_Female_Undead_Lich_76052c24-dd04-4e2d-b8df-3120664c9760
Lizards_Hero_Male_Undead_Lich_6c80cd2c-9bf3-4a82-9a75-20e7f31abcf5
*/

DB_LLLICH_RaceConversion("Dwarf", 1, "Dwarves_Hero_Female_Undead_Lich_414c29bd-6a1a-42ee-a885-f7d449d911f8", "Undead_Dwarf");
DB_LLLICH_RaceConversion("Dwarf", 0, "Dwarves_Hero_Male_Undead_Lich_a9f4b776-dab2-4204-a28c-37fb592dac35", "Undead_Dwarf");
DB_LLLICH_RaceConversion("Elf", 1, "Elves_Hero_Female_Undead_Lich_9dfe13c2-a2f6-4cbb-b341-70f58baded63", "Undead_Elf");
DB_LLLICH_RaceConversion("Elf", 0, "Elves_Hero_Male_Undead_Lich_4305d9f2-1190-4de9-81a5-2c3b10104bd1", "Undead_Elf");
DB_LLLICH_RaceConversion("Human", 1, "Humans_Hero_Female_Undead_Lich_8ff91227-7d47-4f0a-bf68-0a800a6fa250", "Undead_Human");
DB_LLLICH_RaceConversion("Human", 0, "Humans_Hero_Male_Undead_Lich_8ce7cc69-220c-4c49-9723-f28c50750ddf", "Undead_Human");
DB_LLLICH_RaceConversion("Lizard", 1, "Lizards_Hero_Female_Undead_Lich_76052c24-dd04-4e2d-b8df-3120664c9760", "Undead_Lizard");
DB_LLLICH_RaceConversion("Lizard", 0, "Lizards_Hero_Male_Undead_Lich_6c80cd2c-9bf3-4a82-9a75-20e7f31abcf5", "Undead_Lizard");

DB_LLLICH_RacialSkills("Dwarf",     "REALLY_DWARF",     "Target_LLLICH_ParalyzingTouch",    "Target_PetrifyingTouch");
DB_LLLICH_RacialSkills("Human",     "REALLY_HUMAN",     "Shout_LLLICH_DreadShout",          "Shout_InspireStart");
DB_LLLICH_RacialSkills("Elf",       "REALLY_ELF",       "Target_LLLICH_FleshSacrifice",     "Shout_FleshSacrifice");
DB_LLLICH_RacialSkills("Lizard",    "REALLY_LIZARD",    "Cone_LLLICH_Chillbreath",          "Cone_Flamebreath");

DB_LLLICH_LichStatuses("LLLICH_LICHFORM");
DB_LLLICH_LichStatuses("LLLICH_LICHFORM_INFO");
DB_LLLICH_LichStatuses("LLLICH_PHYLACTERY_ACTIVE");
DB_LLLICH_LichStatuses("LLLICH_PHYLACTERY_ACTIVE_INFO");
DB_LLLICH_LichStatuses("LLLICH_PHYLACTERY_NEARLICH");
DB_LLLICH_LichStatuses("LLLICH_CREATE_PHYLACTORY");
DB_LLLICH_LichStatuses("LLLICH_FLESH_SACRIFICE");
KBSECTION

//REGION SETUP
IF
TextEventSet("lichdom_setup")
AND
DB_IsPlayer(_Lich)
AND
CharacterIsControlled(_Lich, 1)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:TextEventSet(lichdom_setup)] Setting up lich.");
LLLich_Main_SetupLich(_Lich);

PROC
LeaderLib_CC_PresetSaved((CHARACTERGUID)_Lich, "LLLICH_Lich")
THEN
LLLich_Main_SetupLich(_Lich);

//LeaderLib PresetMenu
PROC
LeaderLib_PresetApplied((CHARACTERGUID)_Lich, "LLLICH_Lich", 0)
THEN
LLLich_Main_SetupLich(_Lich);

PROC
LLLich_Main_SetupLich((CHARACTERGUID)_Lich)
AND
NOT ObjectGetFlag(_Lich, "LLLICH_Preset_GainedUniqueStaff", 1)
AND
GetItemForItemTemplateInInventory(_Lich, "WPN_LLLICH_Lich_Staff_1H_PresetStarter_461453e9-34bd-4afc-993d-46a09be6eb22", _LichPresetStaff)
AND
GetPosition(_Lich, _x, _y, _z)
AND
CreateItemTemplateAtPosition("WPN_UNIQUE_LLLICH_Lich_Staff_1H_B_7981440d-567e-4738-8779-c36a276b632f", _x, _y, _z, _LichStaff)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:SetupLich] Swapping preset starting staff for unique version.");
ObjectSetFlag(_Lich, "LLLICH_Preset_GainedUniqueStaff");
ItemRemove(_LichPresetStaff);
CharacterEquipItem(_Lich, _LichStaff);

PROC
LLLich_Main_SetupLich((CHARACTERGUID)_Lich)
AND
NOT ObjectGetFlag(_Lich, "LLLICH_Preset_GainedUniqueStaff", 1)
AND
CharacterGetEquippedItem(_Lich, "Weapon", (ITEMGUID)_LichPresetStaff)
AND
_LichPresetStaff != NULL_00000000-0000-0000-0000-000000000000
AND
GetTemplate(_LichPresetStaff, "WPN_LLLICH_Lich_Staff_1H_PresetStarter_461453e9-34bd-4afc-993d-46a09be6eb22")
AND
GetPosition(_Lich, _x, _y, _z)
AND
CreateItemTemplateAtPosition("WPN_UNIQUE_LLLICH_Lich_Staff_1H_B_7981440d-567e-4738-8779-c36a276b632f", _x, _y, _z, _LichStaff)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:SetupLich] Swapping preset starting staff for unique version.");
ObjectSetFlag(_Lich, "LLLICH_Preset_GainedUniqueStaff");
ItemRemove(_LichPresetStaff);
CharacterEquipItem(_Lich, _LichStaff);

PROC
LLLich_Main_SetupLich((CHARACTERGUID)_Lich)
AND
NOT ObjectGetFlag(_Lich, "LLLICH_Preset_GainedInitialSoulExtractor", 1)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:SetupLich] Adding an initial [Soul Extractor].");
ObjectSetFlag(_Lich, "LLLICH_Preset_GainedInitialSoulExtractor");
ItemTemplateAddTo("PUZ_LLLICH_PhylacteryCreator_A_71cb1886-a2fc-4f3a-acd6-886485fa9bee", _Lich, 1, 1);

IF
ObjectFlagSet("LLLICH_Commands_SetupLich", (CHARACTERGUID)_Lich, _)
THEN
ObjectClearFlag(_Lich, "LLLICH_Commands_SetupLich");
LLLich_Main_SetupLich(_Lich);

PROC
LLLich_Main_SetupLich((CHARACTERGUID)_Lich)
AND
NOT ObjectGetFlag(_Lich, "LLLICH_Preset_InitialRaceCheck", 1)
AND
CharacterGetRace(_Lich, 0, _Race)
AND
StringContains(_Race, "Undead", 1)
THEN
ObjectSetFlag(_Lich, "LLLICH_LichRaceOriginallyUndead");
ObjectSetFlag(_Lich, "LLLICH_Preset_InitialRaceCheck");

PROC
LLLich_Main_SetupLich((CHARACTERGUID)_Lich)
AND
NOT ObjectGetFlag(_Lich, "LLLICH_Preset_InitialRaceCheck", 1)
THEN
ObjectSetFlag(_Lich, "LLLICH_Preset_InitialRaceCheck");

QRY
LLLICH_Main_QRY_ShouldAddRaceSkill((CHARACTERGUID)_Lich, (STRING)_RaceTag, (STRING)_RaceSkill)
AND
IsTagged(_Lich, _RaceTag, 1)
THEN
DB_NOOP(1);

QRY
LLLICH_Main_QRY_ShouldAddRaceSkill((CHARACTERGUID)_Lich, (STRING)_RaceTag, (STRING)_RaceSkill)
AND
CharacterHasSkill(_Lich, _RaceSkill, 1)
THEN
DB_NOOP(1);

PROC
LLLich_Main_SetupLich((CHARACTERGUID)_Lich)
AND
DB_LLLICH_RacialSkills(_Race, _RaceTag, _LichSkill, _RaceSkill)
AND
LLLICH_Main_QRY_ShouldAddRaceSkill(_Lich, _RaceTag, _RaceSkill)
THEN
CharacterRemoveSkill(_Lich, _RaceSkill);
CharacterAddSkill(_Lich, _LichSkill, 1);

PROC
LLLich_Main_SetupLich((CHARACTERGUID)_Lich)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:SetupLich] Added [Zombie] talent and [LLLICH_Lich] tag.");
CharacterAddTalent(_Lich, "Zombie");
SetTag(_Lich, "LLLICH_Lich");
SetStoryEvent(_Lich, "LLLICH_SetupLich");
/*
PROC
LLLich_Main_SetupLich((CHARACTERGUID)_Lich)
AND
CharacterHasSkill(_Lich, "Shout_LLLICH_CreatePhylactery", 0)
THEN
CharacterAddSkill(_Lich, "Shout_LLLICH_CreatePhylactery", 1);
*/

PROC
LLLich_Main_SetUndeadTag((CHARACTERGUID)_Lich)
AND
CharacterGetRace(_Lich, 0, _Race)
AND
DB_LLLICH_UndeadConversionTags(_Race, _UndeadTag, _RegularTag)
THEN
SetTag(_Lich, _UndeadTag);
ClearTag(_Lich, _RegularTag);
//END_REGION

//REGION RESET
IF
ObjectFlagSet("LLLICH_Commands_ResetLich", (CHARACTERGUID)_Lich, _)
THEN
ObjectClearFlag(_Lich, "LLLICH_Commands_ResetLich");
LLLich_Debug_ResetLich(_Lich);

PROC
LLLich_Debug_ResetLich((CHARACTERGUID)_Lich)
AND
DB_LLLICH_RacialSkills(_Race, _RaceTag, _LichSkill, _RaceSkill)
AND
CharacterHasSkill(_Lich, _LichSkill, 1)
THEN
CharacterRemoveSkill(_Lich, _LichSkill);
LLLich_Debug_ResetLich_AddOriginalRaceSkill(_Lich, _RaceSkill);

PROC
LLLich_Debug_ResetLich_AddOriginalRaceSkill((CHARACTERGUID)_Lich, (STRING)_RaceSkill)
AND
ObjectGetFlag(_Lich, "LLLICH_LichRaceOriginallyUndead", 0)
THEN
CharacterAddSkill(_Lich, _RaceSkill, 1);

PROC
LLLich_Debug_ResetLich((CHARACTERGUID)_Lich)
AND
ObjectGetFlag(_Lich, "LLLICH_LichRaceOriginallyUndead", 1)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:ResetLich] Removing [Zombie] talent.");
CharacterRemoveTalent(_Lich, "Zombie");

PROC
LLLich_Debug_ResetLich((CHARACTERGUID)_Lich)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:ResetLich] Clearing [LLLICH_Lich] tag and removing status influence [LLLICH_LichForm] from story.");
//CharacterRemoveSkill(_Lich, "Shout_LLLICH_CreatePhylactery");
ClearTag(_Lich, "LLLICH_Lich");
SetStoryEvent(_Lich, "LLLICH_ClearLich");
LLLICH_Phylactery_ClearFlags(_Lich);

PROC
LLLich_Debug_ResetLich((CHARACTERGUID)_Lich)
AND
DB_LLLICH_LichStatuses(_Status)
THEN
RemoveStatus(_Lich, _Status);
//END_REGION

//REGION RACE_CHANGE
IF
TextEventSet("lichdom_racechange")
AND
DB_IsPlayer(_Lich)
AND
CharacterIsControlled(_Lich, 1)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:TextEventSet(lichdom_racechange)] Changing race to an undead lich version.");
LLLich_Main_ChangeRaceToUndead(_Lich);

IF
ObjectFlagSet("LLLICH_Commands_ChangeRaceToLich", (CHARACTERGUID)_Lich, _)
THEN
ObjectClearFlag(_Lich, "LLLICH_Commands_ChangeRaceToLich");
LLLich_Main_ChangeRaceToUndead(_Lich);

PROC
LLLich_Main_ChangeRaceToUndead((CHARACTERGUID)_Lich)
AND
GetTemplate(_Lich, _OriginalTemplate)
AND
CharacterGetRace(_Lich, 0, _Race)
AND
IsTagged(_Lich, "FEMALE", _IsFemale)
AND
DB_LLLICH_RaceConversion(_Race, _IsFemale, _UndeadTemplate, _UndeadRacePreset)
THEN
ObjectSetFlag(_Lich, "LLLICH_LichRaceChanged");
DB_LLLICH_LichData_OriginalRace(_Lich, _OriginalTemplate, _Race, _IsFemale);
DB_LLLICH_Main_Temp_RaceChangeData(_Lich, _IsFemale, _UndeadTemplate, _UndeadRacePreset);
LeaderLib_PresetMenu_StartApplyingPreset(_Lich, _UndeadRacePreset, 1);

//Undead Races
PROC
LLLich_Main_ChangeRaceToUndead((CHARACTERGUID)_Lich)
AND
NOT DB_LLLICH_Main_Temp_RaceChangeData(_Lich, _, _, _)
AND
GetTemplate(_Lich, _OriginalTemplate)
AND
CharacterGetRace(_Lich, 0, _UndeadRace)
AND
IsTagged(_Lich, "FEMALE", _IsFemale)
AND
DB_LLLICH_RaceConversion(_Race, _IsFemale, _UndeadTemplate, _UndeadRace)
THEN
ObjectSetFlag(_Lich, "LLLICH_LichRaceChanged");
DB_LLLICH_LichData_OriginalRace(_Lich, _OriginalTemplate, _Race, _IsFemale);
DB_LLLICH_Main_Temp_RaceChangeData(_Lich, _IsFemale, _UndeadTemplate, _UndeadRace);
//No need to apply a race preset for undead races. Transform instead!
ProcObjectTimer(_Lich, "LLLICH_Timers_Main_TransformToTemplate", 50);

PROC
LeaderLib_PresetApplied((CHARACTERGUID)_Lich, (STRING)_UndeadRacePreset, 1)
AND
DB_LLLICH_Main_Temp_RaceChangeData(_Lich, 1, _UndeadTemplate, _UndeadRacePreset)
THEN
SetTag(_Lich, "FEMALE");
ClearTag(_Lich, "MALE");

PROC
LeaderLib_PresetApplied((CHARACTERGUID)_Lich, (STRING)_UndeadRacePreset, 1)
AND
DB_LLLICH_Main_Temp_RaceChangeData(_Lich, 0, _UndeadTemplate, _UndeadRacePreset)
THEN
SetTag(_Lich, "MALE");
ClearTag(_Lich, "FEMALE");

PROC
LeaderLib_PresetApplied((CHARACTERGUID)_Lich, (STRING)_UndeadRacePreset, 1)
AND
DB_LLLICH_Main_Temp_RaceChangeData(_Lich, _IsFemale, _UndeadTemplate, _UndeadRacePreset)
THEN
ProcObjectTimer(_Lich, "LLLICH_Timers_Main_TransformToTemplate", 150);

//Race presets/transformations may reset the max SP
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lich, "LLLICH_Timers_Main_TransformToTemplate")
AND
CharacterGetMaxSourcePoints(_Lich, _MaxSP)
THEN
DB_LLLICH_Main_Temp_MaxSourcePoints(_Lich, _MaxSP);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lich, "LLLICH_Timers_Main_TransformToTemplate")
AND
DB_LLLICH_Main_Temp_RaceChangeData(_Lich, _IsFemale, _UndeadTemplate, _UndeadRacePreset)
THEN
CharacterTransform(_Lich, _UndeadTemplate, 1, 1, 1, 0, 0, 1, 0);
ProcObjectTimer(_Lich, "LLLICH_Timers_Main_ApplyClassPreset", 150);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lich, "LLLICH_Timers_Main_TransformToTemplate")
AND
DB_LLLICH_Main_Temp_RaceChangeData(_Lich, _IsFemale, _UndeadTemplate, _UndeadRacePreset)
THEN
NOT DB_LLLICH_Main_Temp_RaceChangeData(_Lich, _IsFemale, _UndeadTemplate, _UndeadRacePreset);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lich, "LLLICH_Timers_Main_ApplyClassPreset")
AND
DB_LLLICH_Main_Temp_MaxSourcePoints(_Lich, _MaxSP)
THEN
NOT DB_LLLICH_Main_Temp_MaxSourcePoints(_Lich, _MaxSP);
CharacterOverrideMaxSourcePoints(_Lich, _MaxSP);
LeaderLog_LogInt("DEBUG", "[Lichdom:Main:LLLICH_Timers_Main_ApplyClassPreset] Reset max source points to [",_MaxSP,"].");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lich, "LLLICH_Timers_Main_ApplyClassPreset")
AND
GetVarFixedString(_Lich, "LeaderLib_CharacterCreationPreset", _Preset)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:LLLICH_Timers_Main_ApplyClassPreset] Re-applying CC preset [",_Preset,"].");
DB_LLLICH_Main_Temp_TransformToLich(_Lich, _Preset);
LeaderLib_PresetMenu_StartApplyingPreset(_Lich, _Preset, 0);

//Editor, Existing saves
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lich, "LLLICH_Timers_Main_ApplyClassPreset")
AND
NOT DB_LLLICH_Main_Temp_TransformToLich(_Lich, _)
AND
NOT GetVarFixedString(_Lich, "LeaderLib_CharacterCreationPreset", _)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:LLLICH_Timers_Main_ApplyClassPreset] No CC preset found. Applying preset [LLLICH_Lich].");
DB_LLLICH_Main_Temp_TransformToLich(_Lich, "LLLICH_Lich");
LeaderLib_PresetMenu_StartApplyingPreset(_Lich, "LLLICH_Lich", 0);

PROC
LeaderLib_PresetApplied((CHARACTERGUID)_Lich, (STRING)_Preset, 0)
AND
DB_LLLICH_Main_Temp_TransformToLich(_Lich, _Preset)
THEN
NOT DB_LLLICH_Main_Temp_TransformToLich(_Lich, _Preset);
//LLLich_Main_SetupLich(_Lich);
//END_REGION

//REGION RESET_RACE
IF
TextEventSet("lichdom_racereset")
AND
DB_IsPlayer(_Lich)
AND
CharacterIsControlled(_Lich, 1)
THEN
LeaderLog_Log("DEBUG", "[Lichdom:Main:TextEventSet(lichdom_racereset)] Resetting race to original template.");
LLLich_Main_ResetRace(_Lich);

IF
ObjectFlagSet("LLLICH_Commands_ResetRace", (CHARACTERGUID)_Lich, _)
THEN
ObjectClearFlag(_Lich, "LLLICH_Commands_ResetRace");
LLLich_Main_ResetRace(_Lich);

PROC
LLLich_Main_ResetRace((CHARACTERGUID)_Lich)
AND
DB_LLLICH_LichData_OriginalRace(_Lich, _OriginalTemplate, _Race, _IsFemale)
AND
StringContains(_Race, "Undead", 0)
THEN
NOT DB_LLLICH_LichData_OriginalRace(_Lich, _OriginalTemplate, _Race, _IsFemale);
DB_LLLICH_Main_Temp_RaceResetData(_Lich, _IsFemale, _OriginalTemplate, _Race);
LeaderLib_PresetMenu_StartApplyingPreset(_Lich, _Race, 1);

//Undead Races
PROC
LLLich_Main_ResetRace((CHARACTERGUID)_Lich)
AND
DB_LLLICH_LichData_OriginalRace(_Lich, _OriginalTemplate, _Race, _IsFemale)
AND
StringContains(_Race, "Undead", 1)
THEN
NOT DB_LLLICH_LichData_OriginalRace(_Lich, _OriginalTemplate, _Race, _IsFemale);
DB_LLLICH_Main_Temp_RaceResetData(_Lich, _IsFemale, _OriginalTemplate, _Race);
ProcObjectTimer(_Lich, "LLLICH_Timers_Main_ResetRaceTransform", 50);

PROC
LeaderLib_PresetApplied((CHARACTERGUID)_Lich, (STRING)_Race, 1)
AND
DB_LLLICH_Main_Temp_RaceResetData(_Lich, 1, _OriginalTemplate, _Race)
THEN
SetTag(_Lich, "FEMALE");
ClearTag(_Lich, "MALE");

PROC
LeaderLib_PresetApplied((CHARACTERGUID)_Lich, (STRING)_Race, 1)
AND
DB_LLLICH_Main_Temp_RaceResetData(_Lich, 0, _OriginalTemplate, _Race)
THEN
ClearTag(_Lich, "FEMALE");
SetTag(_Lich, "MALE");

PROC
LeaderLib_PresetApplied((CHARACTERGUID)_Lich, (STRING)_Race, 1)
AND
DB_LLLICH_Main_Temp_RaceResetData(_Lich, _IsFemale, _OriginalTemplate, _Race)
THEN
ProcObjectTimer(_Lich, "LLLICH_Timers_Main_ResetRaceTransform", 150);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lich, "LLLICH_Timers_Main_ResetRaceTransform")
AND
CharacterGetMaxSourcePoints(_Lich, _MaxSP)
THEN
DB_LLLICH_Main_Temp_MaxSourcePoints(_Lich, _MaxSP);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Lich, "LLLICH_Timers_Main_ResetRaceTransform")
AND
DB_LLLICH_Main_Temp_RaceResetData(_Lich, _IsFemale, _OriginalTemplate, _Race)
THEN
NOT DB_LLLICH_Main_Temp_RaceResetData(_Lich, _IsFemale, _OriginalTemplate, _Race);
CharacterTransform(_Lich, _OriginalTemplate, 1, 1, 1, 0, 0, 1, 0);
ObjectClearFlag(_Lich, "LLLICH_LichRaceChanged");
ProcObjectTimer(_Lich, "LLLICH_Timers_Main_ApplyClassPreset", 150);
//END_REGION

//REGION STATUS_PRESERVATION
PROC
LeaderUpdater_ModUpdated("Lichdom", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 0, 9, 1, 2)
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player, "LLLICH_Lich", 1)
THEN
LLLICH_Main_Internal_LichUpdate(_Player);

PROC
LLLICH_Main_Internal_LichUpdate((CHARACTERGUID)_Lich)
AND
NOT HasActiveStatus(_Lich, "LLLICH_LICHFORM", 1)
THEN
SetStoryEvent(_Lich, "LLLICH_ApplyStatusInfluence");

PROC
LLLICH_Main_Internal_LichUpdate((CHARACTERGUID)_Lich)
AND
DB_LLLICH_Phylactery_Active(_Lich, _Phylactery)
AND
ObjectExists(_Phylactery, 1)
AND
ItemIsDestroyed(_Phylactery, 0)
THEN
SetStoryEvent(_Lich, "LLLICH_ApplyPhylacteryStatusInfluence");

PROC
LLLICH_Main_Internal_LichUpdate((CHARACTERGUID)_Lich)
AND
NOT DB_LLLICH_Phylactery_Active(_Lich, _)
THEN
ObjectClearFlag(_Lich, "LLLICH_PhylacteryActive");
ObjectClearFlag(_Lich, "LLLICH_Phylactery_Amulet");
ObjectClearFlag(_Lich, "LLLICH_Phylactery_Ring");
ObjectClearFlag(_Lich, "LLLICH_Phylactery_Smallbox");

IF
RegionEnded(_Region)
AND
DB_IsPlayer(_Player)
AND
IsTagged(_Player, "LLLICH_Lich", 1)
THEN
DB_LLLICH_Temp_ActiveLiches(_Player);

IF
GameStarted(_Level, _)
AND
DB_LLLICH_Temp_ActiveLiches(_Lich)
THEN
LLLICH_Main_ApplyLichStatuses(_Lich);
NOT DB_LLLICH_Temp_ActiveLiches(_Lich);

PROC
LLLICH_Main_ApplyLichStatuses((CHARACTERGUID)_Lich)
AND
NOT HasActiveStatus(_Lich, "LLLICH_LICHFORM", 1)
THEN
SetStoryEvent(_Lich, "LLLICH_ApplyStatusInfluence");

PROC
LLLICH_Main_ApplyLichStatuses((CHARACTERGUID)_Lich)
AND
NOT HasActiveStatus(_Lich, "LLLICH_PHYLACTERY_ACTIVE", 1)
AND
DB_LLLICH_Phylactery_Active(_Lich, _Phylactery)
AND
ObjectExists(_Phylactery, 1)
AND
CharacterIsDead(_Lich, 0)
AND
ItemIsDestroyed(_Phylactery, 0)
THEN
SetStoryEvent(_Lich, "LLLICH_ApplyPhylacteryStatusInfluence");

IF
TextEventSet("lichdom_statustest")
AND
CharacterGetHostCharacter(_Host)
THEN
RemoveHarmfulStatuses(_Host);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Lichdom"