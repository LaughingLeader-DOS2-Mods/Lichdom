Version 1
SubGoalCombiner SGC_AND
INITSECTION

//Missing anim flags
DB_GLO_CharacterAnimation("PlayAnim_skill_prepare_flight_01_start","skill_prepare_flight_01_start", 0);
DB_GLO_CharacterAnimation("PlayAnim_skill_prepare_flight_01_loop","skill_prepare_flight_01_loop", 1);
DB_GLO_CharacterAnimation("PlayAnim_skill_jump_flight_start","skill_jump_flight_start", 0);

//LeaderLib_Statuses_Register_PermanentStatus("Lichdom", "LichPassives", "LLLICH_LICHFORM", (STRING)_Flag)
KBSECTION

QRY
LLLICH_Settings_ShouldRefundSoulExtractor((CHARACTERGUID)_Lich)
AND
DB_LLLICH_Phylactery_Active(_Lich, _Item, _Active)
AND
ObjectExists(_Item, 0)
THEN
DB_NOOP(1);

QRY
LLLICH_Settings_ShouldRefundSoulExtractor((CHARACTERGUID)_Lich)
AND
NOT DB_LLLICH_Phylactery_Active(_Lich, _, _)
AND
HasActiveStatus(_Lich, "LLLICH_PHYLACTERY_ACTIVE", 1)
THEN
DB_NOOP(1);

QRY
LLLICH_Settings_ShouldRefundSoulExtractor((CHARACTERGUID)_Lich)
AND
IsTagged(_Lich, "LLLICH_Lich", 1)
AND
NOT ObjectGetFlag(_Lich, "LLLICH_Preset_GainedInitialSoulExtractor", 1)
THEN
DB_NOOP(1);

IF
DialogStarted("LLLICH_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Lich)
AND
IsTagged(_Lich, "LLLICH_Lich", 1)
AND
LLLICH_Settings_ShouldRefundSoulExtractor(_Lich)
THEN
ObjectSetFlag(_Lich, "LLLICH_Settings_SoulExtractorRefundAvailable");

IF
DialogEnded("LLLICH_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Lich)
AND
ObjectGetFlag(_Lich, "LLLICH_Settings_SoulExtractorRefundAvailable", 1)
THEN
ObjectClearFlag(_Lich, "LLLICH_Settings_SoulExtractorRefundAvailable");

IF
ObjectFlagSet("LLLICH_Commands_RefundSoulExtractor", (CHARACTERGUID)_Lich, _)
THEN
ObjectClearFlag(_Lich, "LLLICH_Commands_RefundSoulExtractor");
ObjectClearFlag(_Lich, "LLLICH_Settings_SoulExtractorRefundAvailable");
LLLich_Debug_ResetLich(_Lich);

IF
ObjectFlagSet("LLLICH_Commands_RefundSoulExtractor", (CHARACTERGUID)_Lich, _)
AND
ItemTemplateIsInCharacterInventory(_Lich, "PUZ_LLLICH_PhylacteryCreator_A_71cb1886-a2fc-4f3a-acd6-886485fa9bee", 0)
THEN
ItemTemplateAddTo("PUZ_LLLICH_PhylacteryCreator_A_71cb1886-a2fc-4f3a-acd6-886485fa9bee", _Lich, 1, 1);

IF
ObjectFlagSet("LLLICH_Commands_RefreshStatuses", (CHARACTERGUID)_Lich, _)
THEN
ObjectClearFlag(_Lich, "LLLICH_Commands_RefreshStatuses");
SetStoryEvent(_Lich, "LLLICH_ApplyStatusInfluence");

IF
ObjectFlagSet("LLLICH_Commands_RefreshStatuses", (CHARACTERGUID)_Lich, _)
AND
DB_LLLICH_Phylactery_Active(_Lich, _Phylactery, 1)
THEN
SetStoryEvent(_Lich, "LLLICH_ApplyPhylacteryStatusInfluence");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Lichdom"