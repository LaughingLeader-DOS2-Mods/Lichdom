Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION
/*
PROC
LLLICH_Statuses_OnLichStatusApplied((CHARACTERGUID)_Lich, (STRING)_Status, (INTEGER)_VarA, (INTEGER)_VarB)
THEN
DB_NOOP(1);
*/

//REGION PENALTY
IF
CharacterStatusApplied(_Lich, "LLLICH_LICHFORM", _)
AND
HasActiveStatus(_Lich, "LLLICH_PHYLACTERY_ACTIVE", _PhylacteryActive)
AND
GlobalGetFlag("LLLICH_Settings_DisableStatusPenalty", _DisablePenalty)
THEN
LLLICH_Statuses_OnLichFormApplied(_Lich, _PhylacteryActive, _DisablePenalty);

PROC
LLLICH_Statuses_OnLichFormApplied((CHARACTERGUID)_Lich, 1, 0)
THEN
ApplyStatus(_Lich, "LLLICH_LICHFORM_PENALTY_A", -1.0, 1, _Lich);

PROC
LLLICH_Statuses_OnLichFormApplied((CHARACTERGUID)_Lich, 0, 0)
THEN
ApplyStatus(_Lich, "LLLICH_LICHFORM_PENALTY_B", -1.0, 1, _Lich);

IF
CharacterStatusApplied(_Lich, "LLLICH_PHYLACTERY_ACTIVE", _)
AND
GlobalGetFlag("LLLICH_Settings_DisableStatusPenalty", 0)
THEN
ApplyStatus(_Lich, "LLLICH_LICHFORM_PENALTY_A", -1.0, 1, _Lich);

IF
CharacterStatusRemoved(_Lich, "LLLICH_LICHFORM", _)
THEN
RemoveStatus(_Lich, "LLLICH_LICHFORM_PENALTY_A");
RemoveStatus(_Lich, "LLLICH_LICHFORM_PENALTY_B");
LLLICH_Statuses_OnLichStatusRemoved(_Lich, "LLLICH_LICHFORM");
//END_REGION

//REGION INFO_STATUS
PROC
LLLICH_Statuses_OnLichFormApplied((CHARACTERGUID)_Lich, (INTEGER)_VarA, (INTEGER)_VarB)
AND
ObjectGetFlag(_Lich, "LLLICH_Settings_HideLichStatus", 0)
AND
NOT LeaderLib_Helper_QRY_HasStatus(_Lich, "LLLICH_LICHFORM_INFO")
THEN
ApplyStatus(_Lich, "LLLICH_LICHFORM_INFO", -1.0, 0, _Lich);

IF
CharacterStatusRemoved(_Lich, "LLLICH_LICHFORM_INFO", _)
AND
ObjectGetFlag(_Lich, "LLLICH_Settings_HideLichStatus", 0)
AND
LeaderLib_Helper_QRY_HasStatus(_Lich, "LLLICH_LICHFORM")
THEN
ApplyStatus(_Lich, "LLLICH_LICHFORM_INFO", -1.0, 0, _Lich);

PROC
LLLICH_Statuses_OnLichStatusRemoved((CHARACTERGUID)_Lich, "LLLICH_LICHFORM")
AND
HasActiveStatus(_Lich, "LLLICH_LICHFORM_INFO", 1)
THEN
RemoveStatus(_Lich, "LLLICH_LICHFORM_INFO");

IF
ObjectFlagSet("LLLICH_Settings_HideLichStatus", (CHARACTERGUID)_Lich, _)
AND
HasActiveStatus(_Lich, "LLLICH_LICHFORM_INFO", 1)
THEN
RemoveStatus(_Lich, "LLLICH_LICHFORM_INFO");
//END_REGION

IF
CharacterStatusApplied(_Lich, "LLLICH_CHILLFORM", _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Lich, "LLLICH_ChillFormActive", "Lichdom");

/* [OSITOOLS_ONLY]
IF
CharacterStatusApplied(_Target, "LLLICH_TRY_DOMINATE", _Source)
THEN
NRD_ModCall("Lichdom", "TryDominateUndead", (STRING)_Target, (STRING)_Source);
*/

EXITSECTION
ENDEXITSECTION
ParentTargetEdge "LLLICH_11_GameScripts"